grammar org.xtext.de.htwg.Plugin with org.eclipse.xtext.common.Terminals

generate plugin "http://www.xtext.org/de/htwg/Plugin"

Plugin:
	elements+=AbstractElement*
;

AbstractElement:
	PackageDeclaration | Type | Import
;

QualifiedName:
	ID ('.' ID)*
;

QualifiedNameWithWildcard:
	QualifiedName '.*'?
;

Type:
	Typedef | Model | Controller | Database | Enumeration
;

Typedef:
	'type' name=ID
;

PackageDeclaration:
	'package' name=QualifiedName '{'
		elements+=AbstractElement*
	'}'
;

Import:
	'import' importedNamespace = QualifiedNameWithWildcard
;

Model:
	'model' name=ID ('extends' superType=[Model | QualifiedName])? '{'
		properties+=MyProperty*
		methods+=Method*
	'}'
;

Controller:
	'controller' name=ID ('extends' superType=[Controller | QualifiedName])? '{'
		methods+=Method*
	'}'
;

Database:
	'database' name=ID ('extends' superType=[Database | QualifiedName])? '{'
		methods+=Method*
	'}'
;

Enumeration:
	'enum' name=ID '{'
		(enumValues += ID
			(',' enumValues += ID)?)+
	'}'
;

MyProperty:
	'property' name=ID ':' type= [Type | QualifiedName]
;

Method:
	'method' name=ID 
		'('(params+=Parameter 
			(',' params+=Parameter)*)?')'
		':' type=[Type | QualifiedName]
;

Parameter:
	name=ID ':' type= [Type | QualifiedName]
;