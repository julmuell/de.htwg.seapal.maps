grammar org.xtext.de.htwg.Plugin with org.eclipse.xtext.common.Terminals

generate plugin "http://www.xtext.org/de/htwg/Plugin"

Plugin:
	'PACKAGE' name=QualifiedName
	elements+=RootElement*
;

RootElement:
	PackageDeclaration | Type | Import
;

AbstractElement:
	Type | Import
;

QualifiedName:
	ID ('.' ID)*
;

QualifiedNameWithWildcard:
	QualifiedName '.*'?
;

Type:
	Typedef | Interface | Enumeration
;

Typedef:
	'type' name=ID
;

PackageDeclaration:
	'package' name=QualifiedName '{'
		elements+=AbstractElement*
	'}'
;

Import:
	'import' importedNamespace = QualifiedNameWithWildcard
;

Interface:
	'interface' name=ID '{'
		properties+=MyProperty*
		methods+=Method*
	'}'
;

Enumeration:
	'enum' name=ID '{'
		(enumValues += ID
			(',' enumValues += ID)?)*
	'}'
;

MyProperty:
	'property' name=ID ':' type= [Type | QualifiedName]
;

Method:
	'method' name=ID 
		'('(params+=Parameter 
			(',' params+=Parameter)*)?')'
		':' type=[Type | QualifiedName]
;

Parameter:
	name=ID ':' type= [Type | QualifiedName]
;